#!/usr/bin/env python

Import("env")
Import("env_modules")

env_module = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

thirdparty_dir = "thirdparty/lz4/"
thirdparty_sources = [
    "xxhash.c",
    "lz4hc.c",
    "lz4frame.c",
    "lz4.c",
]
thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

env_module.Append(CPPDEFINES=["LZ4_USER_MEMORY_FUNCTIONS"])
env_module.Append(CPPPATH=[thirdparty_dir])

env_thirdparty = env_module.Clone()
env_thirdparty.disable_warnings()
env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_module.add_source_files(module_obj, "*.cpp")
if env.editor_build:
    env_module.add_source_files(module_obj, "editor/*.cpp")

env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
